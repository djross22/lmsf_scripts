//isPipetting is a variable passed to the Hamilton Venus method; if isPipetting is true, the STAR will pipette liquids; otherwise the STAR just moves plates around
Set(isPipetting, true)
//isDebugging controls whether or not user debugging prompts/messages are displayed (both in LMSF and Hamilton)
Set(isDebugging, false)

//No DNA extraction for this version of the script
Set(keepOldGrowthPlate, false)

//manualSeal controls whether sealing is manual or automatic
//Set(manualSeal, Yes)
Set(manualSeal, No)

/////////////////////////////////////////////////////////////////
//Read in commonly used directories
ReadScript(C:\Users\PAA\Documents\LMSF Scheduler\Common protocol scripts\Set Common Directories.lmsf)
/////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////
//Set directories for  .hsl, .opv, and .prt files that are specifc to this experiment
Set(hamiltonMethodDir, C:\Program Files (x86)\Hamilton\Methods\GSF-IMS Project\E. coli Methods\Cytometry for 6 variants)
Set(ovpDirectory, C:\Program Files (x86)\PAA\Overlord3\Procedures\GSF-IMS\E. coli\Fitness Selection 1)
Set(scriptDir, C:\Users\PAA\Documents\LMSF Scheduler\GSF-IMS Project\E. coli\Cytometry for 6 variants)
/////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////
//Hamilton Venus Methods, in the order that they get run
Set(startMethod, Uniform non-selective plate prep plus bacteria.1 strain.6 plasmids.hsl)
Set(nonSelectiveGradientMethod, Inducer gradient plate prep.hsl)
Set(preHeatPlateMethod, Pre-heat growth plate.hsl)
//Plate pre-heating method takes 12m:16s to run
Set(preHeatPlateTime, 736)
Set(copyPlateMethod, Dilute-copy plate x-fold with 96-head.hsl)
Set(cytometryPrepMethod, Prep cytometry plate.hsl)
Set(cytometryTransferMethod, Transfer cells from old GP to Cytometry Plate.hsl)
//Venus method constrains 200<=cultureVolume<=500
Set(cultureVolume, 500)


/////////////////////////////////////////////////////////////////
//Gen5 protcols to be run and associated timers:
//Set(overNightProtocol, {gen5ProtDir}\GSF-IMS-E-Coli\Growth Plate w Membrane 16h.37C.1C.Ex500Em530.prt)
Set(overNightProtocol, {gen5ProtDir}\GSF-IMS-E-Coli\Growth Plate w Membrane 12h.37C.1C.Ex500Em530.prt)
//overNightProtocolTime = nominal run time + 300
//Set(overNightProtocolTime, 57900)
Set(overNightProtocolTime, 43500)

Set(growOutProtocol, {gen5ProtDir}\GSF-IMS-E-Coli\Growth Plate w Membrane 4h.35m.37C.1C.Ex500Em530.prt)
//growOutProtocolTime = nominal run time + 300
Set(growOutProtocolTime, 16800)

//Gen5 protocol for testing readers at beginning of run to give consistent starting state
Set(initProtocol, {gen5ProtDir}\Growth Plate w Membrane short test.prt)
/////////////////////////////////////////////////////////////////

//Set readers to be used
Set(mainReader, Neo)

/////////////////////////////////////
//This section sets shorter Gen5 protocols for testing, set isTesting to false for actual experiment
Set(isTesting, false)
GetUserYesNo(isTesting, Testing?, Is this a test run (short Gen5 protocols)?)

Set(testProtocol, {gen5ProtDir}\GSF-IMS-E-Coli\Growth Plate w Membrane 1h.37C.1C.prt)
If({isTesting}==Yes, Set(overNightProtocol, {gen5ProtDir}\GSF-IMS-E-Coli\Growth Plate w Membrane 35m.37C.1C.prt))
//overNightProtocolTime = nominal run time + 300
If({isTesting}==Yes, Set(overNightProtocolTime, 2400))
If({isTesting}==Yes, Set(growOutProtocol, {gen5ProtDir}\GSF-IMS-E-Coli\Growth Plate w Membrane 30m.37C.1C.prt))
//growOutProtocolTime = nominal run time + 300
If({isTesting}==Yes, Set(growOutProtocolTime, 2100))
/////////////////////////////////////////

/////////////////////////////////////////
//Set any other short-hand dictionary keys that will be used repeatedly
Set(getNewGrowthPlate, {ovpCommDirectory}\Labware\Move Tips and New Growth Plate to STAR.ovp)
/////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////
//User input and Overlord initialization section

StartPrompt(E. Coli Fitness Selection, {scriptDir}\Cytometry.1 strain.6 plasmids-list.txt)

UserPrompt(Check STAR Hamilton Run Control, Make sure that the Hamilton Run Control program on the STAR computer is NOT in simulation mode.)
UserPrompt(Check STAR Hamilton Tips, Make sure there are at least 30 1000 uL tips in the tip supports on the deck of the STAR., {lmsfImageDirectory}\Tip supports 1000.PNG)
UserPrompt(Check STAR Hamilton Tips, Make sure there are at least 96 300 uL tips in the tip supports on the deck of the STAR., {lmsfImageDirectory}\Tip supports 300.PNG)

Set(msg2, Make sure there is at least one rack of 50 uL tips in the PAA Carousel (hotel no. 3).\n)
UserPrompt(Tips, {msg2}, {lmsfImageDirectory}\Tip Hotel 3.png)
Set(tips50Index, 1)

NewXML(growth plate prep)

Overlord({ovpCommDirectory}\Initialize Robot Carousel Sealer and Pealer.ovp)

Get(user, user)

Get(media, media)

UserPrompt(Strain and Plasmids, Open the file strain_plasmids.txt and edit it to contain the appropriate information for this experiment)
ImportDictionary({scriptDir}\strain_plasmids.txt)

AddXML(strains, strain)
AddXML(strain, strainId, strain1)
AddXML(strain, plasmidId, plasmidB)
AddXML(strain, plasmidId, plasmidC)
AddXML(strain, plasmidId, plasmidD)
AddXML(strain, plasmidId, plasmidE)
AddXML(strain, plasmidId, plasmidF)
AddXML(strain, plasmidId, plasmidG)

//Get(strain, strain1, Select the bacteria strain used for the experiment:)
//Get(plasmid, plasmidB, Select the plasmid used for the bacteria in row B of the plate:)
//Get(plasmid, plasmidC, Select the plasmid used for the bacteria in row C of the plate:)
//Get(plasmid, plasmidD, Select the plasmid used for the bacteria in row D of the plate:)
//Get(plasmid, plasmidE, Select the plasmid used for the bacteria in row E of the plate:)
//Get(plasmid, plasmidF, Select the plasmid used for the bacteria in row F of the plate:)
//Get(plasmid, plasmidG, Select the plasmid used for the bacteria in row G of the plate:)

Get(additive, inducer, Select the inducer used for the experiment:)
Get(concentration, inducerStock, Enter the concentration of the {inducer} starting stock:)

Get(note, note)

GetExpID({startDate}_{inducer}-Cytom-6-plasmids)

//review experiment ok/abort dialog
Set(msg1, Review the following, then click 'OK' to accept or 'Abort' to abort and start over:\n\n)
Set(msg2, \tMedia: \t\t\t{media}\n\n)
Set(msg3, \tBacteria strain: \t\t{strain1}, with {plasmid1}\n)
Set(msg4, \t\tPlasmidB: \t\t\t{plasmidB}\n)
Set(msg5, \t\tPlasmidC: \t\t\t{plasmidC}\n)
Set(msg6, \t\tPlasmidD: \t\t\t{plasmidD}\n)
Set(msg7, \t\tPlasmidE: \t\t\t{plasmidE}\n)
Set(msg8, \t\tPlasmidF: \t\t\t{plasmidF}\n)
Set(msg9, \t\tPlasmidG: \t\t\t{plasmidG}\n\n)

Set(msg10, \tInducer: \t\t\t{inducer}, stock concentration: {inducerStock}\n\n)
Set(msg11, \tExperiment ID: \n\t\t{experimentId}\n \tData directory: \n\t\t{dataDirectory}\n\n \tNotes:\n\t\t{note})
UserPrompt(Review Experiment Details: {experimentId}, {msg1}{msg2}{msg3}{msg4}{msg5}{msg6}{msg7}{msg8}{msg9}{msg10}{msg11})

Set(msg1, Set up the STAR deck layout as shown.)
UserPrompt(STAR Deck Layout, {msg1}, {scriptDir}\STAR Deck Layout.with cytometry.PNG, 1000)

Set(msg1, Close the front cover on the STAR.\n)
Set(msg2, Ensure that the Robot arm is in a safe position and that the Guard Override key is in the 'Off' position.\n)
Set(msg3, Check that all the doors of the S-Cell system are closed.\n)
Set(msg4, Then click 'OK' to start the experiment.\n)
UserPrompt(Ready to Start, {msg1}{msg2}{msg3}{msg4}, {lmsfImageDirectory}\dialog-warning.png)

//Lid movement test
UserPrompt(Lid Movement Test, Switch to the STAR computer and observe the lid movement test.)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\Pre-run lid test.hsl)
Timer(100)
WaitFor(Timer)
WaitFor(S-Cell-STAR)
UserPrompt(Lid Movement Test, If the STAR successfully moved the lids click 'OK' to start the experiment.)
// end lid movement test


WaitFor(Overlord)
//End User input and Overlord initialization section
//////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////////
//Run the protocol
////////////////////////////////////////////////////////////////////////////////////

//--- 0 ------------------------------------------------------------------------------

//Test readers while 1st plate is pipetting
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)
Set(msg, Plate reader {mainReader} should have its carrier out. \nVerify that the carrier is out and empty, then Click 'OK' to continue.)
UserPrompt(Check Readers, {msg})
Gen5({mainReader}, RunExp, {initProtocol}, pre-run-test, {dataDirectory})

Set(msg, Switch to STAR computer to edit tip counters, then Click 'OK' to continue.)
UserPrompt(Check Readers, {msg})
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonTipDir}\Edit Tip Counters.hsl)
WaitFor(S-Cell-STAR)

//--- Plate 1 ------------------------------------------------------------------------------
Set(plateNum, 1)
Set(platesInStack, 2)

Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack})
Math(platesInStack, {platesInStack} - 1)
WaitFor(Overlord)

Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{plateNum}.csv)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{startMethod})
Timer(600)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

WaitFor({mainReader}, false)

Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{mainReader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({mainReader}, RunExp, {overNightProtocol}, {experimentId}_growth-plate_{plateNum}a, {dataDirectory})
GetTimeNow(readStartTime)
Math(readEndTime, {readStartTime} + {overNightProtocolTime})

//--- Plate 2 ----------------------------------------------------------------------------- 
Math(plateNum, {plateNum} + 1)

//Move new plate and tips before WaitFor(Timer) so user can make sure there is no Overlord error
Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack} [STAR.Plate.Handling.Labware.Name] "Tips50" [STAR.Tip.50.Index] {tips50Index})
Math(tips50Index, {tips50Index} - 1)
Math(platesInStack, {platesInStack} - 1)
WaitFor(Overlord)

//Wait for reader to get close to finished before starting pipetting method
Math(pipetteStartTime, {readEndTime} - 2220)
Timer({pipetteStartTime})
WaitFor(Timer)

Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{plateNum}.csv)
Set(dilutionFactor, 200)
Set(gradPlateNum, 1)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{nonSelectiveGradientMethod})

Math(preHeatStartTime, {readEndTime} - {preHeatPlateTime})
Timer({preHeatStartTime})
WaitFor(Timer)

WaitFor(S-Cell-STAR)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{preHeatPlateMethod})
WaitFor(S-Cell-STAR)

WaitFor({mainReader})
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{mainReader}")
WaitFor(Overlord)

Gen5({mainReader}, CarrierIn)
WaitFor({mainReader}, false)

Set(keepOldGrowthPlate, false)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{copyPlateMethod})
WaitFor(S-Cell-STAR)

Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{mainReader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({mainReader}, RunExp, {growOutProtocol}, {experimentId}_growth-plate_{plateNum}a, {dataDirectory})
GetTimeNow(readStartTime)
Math(readEndTime, {readStartTime} + {growOutProtocolTime})


//--- Cytometry Plate (already on the STAR deck) ---------------------------------------------------------------------------------

//Set up timer to wait for reader to get close to finished before starting pipetting method
Math(pipetteStartTime, {readEndTime} - 2220)
Math(cytometryStartTime, {pipetteStartTime} - 300)

Timer({cytometryStartTime})
WaitFor(Timer)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{cytometryPrepMethod})
WaitFor(S-Cell-STAR)

WaitFor({mainReader})
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{mainReader}")
WaitFor(Overlord)

Gen5({mainReader}, CarrierIn)
WaitFor({mainReader}, false)

Set(worklistFile, {dataDirectory}\{experimentId}_cytom-plate_{plateNum}.csv)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{cytometryTransferMethod})
WaitFor(S-Cell-STAR)


//--- Clean up --------------------------------------------------------------------------------------

Overlord({ovpCommDirectory}\Sealer Heater Off.ovp)
WaitFor(Overlord)

Timer({readEndTime})
WaitFor(Timer)

WaitFor({mainReader})

SaveXML()

CopyRemoteFiles()
