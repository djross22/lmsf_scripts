//Sub-script for plates 3, 4, 5...

Set(pipettingCushionTime, 2400)
Set(repeatDilutionFactor, 10)

#InsertVariables

//Move new plate and tips before WaitFor(Timer) so user can make sure there is no Overlord error
Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack} [STAR.Plate.Handling.Labware.Name] "Tips300" [STAR.Tip.300.Index] {tips300Index} [STAR.Tip.ClearWaste] "{clear300TipWaste}")
WaitFor(Overlord)
Math(tips300Index, {tips300Index} - 1)
Math(platesInStack, {platesInStack} - 1)


//Wait for reader to get close to finished before starting pipetting method
Math(pipetteWaitTime, {preHeatPlateTime} + {selectivePlateTime})
Math(pipetteWaitTime, {pipetteWaitTime} + {pipettingCushionTime})
Math(pipetteStartTime, {readEndTime} - {pipetteWaitTime})
Timer({pipetteStartTime})
WaitFor(Timer)

Set(oldPlateNum, {plateNum})
Math(plateNum, {plateNum} + 1)
Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{plateNum}.csv)
Set(dilutionFactor, {repeatDilutionFactor})
Math(gradPlateNum, {gradPlateNum} + 1)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{selectiveGradientMethod})
Timer(1000)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

Math(preHeatStartTime, {readEndTime} - {preHeatPlateTime})
Timer({preHeatStartTime})
WaitFor(Timer)

RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{preHeatPlateMethod})

// Run the single-read during the pre-heat
Timer({readEndTime})
WaitFor(Timer)

WaitFor({mainReader})
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to Reader.ovp, [Gen5.Reader.Name] "{mainReader}" [Gen5.Reader2.Name] "{mainReader}")
WaitFor(Overlord)

Gen5({mainReader}, RunExp, {singleReadProtocol}, {experimentId}_single-read_{oldPlateNum}a, {dataDirectory})
Timer({singleReadProtocolTime})
WaitFor(Timer)

// Now wait for pre-heat to finish
WaitFor(S-Cell-STAR)

WaitFor({mainReader})
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

// Plate is already peeled
Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.No Peel.ovp, [Gen5.Reader.Name] "{mainReader}")
WaitFor(Overlord)

Gen5({mainReader}, CarrierIn)
WaitFor({mainReader}, false)

Set(keepOldGrowthPlate, false)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{copyPlateMethod})
Timer(180)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{mainReader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({mainReader}, RunExp, {repeatProtocol}, {experimentId}_growth-plate_{plateNum}a, {dataDirectory})
GetTimeNow(readStartTime)
Math(readEndTime, {readStartTime} + {repeatProtocolTime})

