//preps and runs Plate 3 for each set of plates (aka lineage)
//This method needs to have the reader variable set to a plate reader name
//        and the readEndTime variable set to either {readEndTime1} or {readEndTime2}
#InsertVariables

Math(oldPlateNum, {plateNum} - 1)

//Move new plate and tips before WaitFor(Timer) so user can make sure there is no Overlord error
Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack} [STAR.Plate.Handling.Labware.Name] "Tips300" [STAR.Tip.300.Index] {tips300Index} [STAR.Tip.ClearWaste] "true")
WaitFor(Overlord)
Math(tips300Index, {tips300Index} - 1)
Math(platesInStack, {platesInStack} - 1)

//Wait for reader to get close to finished before starting pipetting method
Math(pipetteStartTime, {readEndTime} - 2700)
Timer({pipetteStartTime})
WaitFor(Timer)

Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{lineage}-{plateNum}.csv)
Set(dilutionFactor, 10)
//gradPlateNum is the number of the gradient plate within a plate lineage, used to set pipetting volumes so that final inducer concentration is correct
Set(gradPlateNum, 2)
//sequenceNum is the total number of gradient plates mixed
//    used by the Hamilton Venus program to determine which column in the Abgene plate to use for starting the inducer gradient
Math(sequenceNum, {sequenceNum} + 1)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{nonSelectiveGradientMethod})
Timer(900)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

Math(preHeatStartTime, {readEndTime} - {preHeatPlateTime})
Math(preHeatStartTime, {preHeatStartTime} + {singleReadProtocolTime})
Timer({preHeatStartTime})
WaitFor(Timer)

RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{preHeatPlateMethod})

// Run the single-read during the pre-heat
Timer({readEndTime})
WaitFor(Timer)

WaitFor({reader})
Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to Reader.ovp, [Gen5.Reader.Name] "{reader}" [Gen5.Reader2.Name] "{reader}")
WaitFor(Overlord)

Gen5({reader}, RunExp, {singleReadProtocol}, {experimentId}_single-OD_{lineage}-{oldPlateNum}_f, {dataDirectory})
Timer({singleReadProtocolTime})
WaitFor(Timer)

// Now wait for pre-heat to finish
WaitFor(S-Cell-STAR)

WaitFor({reader})
Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.No Peel.ovp, [Gen5.Reader.Name] "{reader}")
WaitFor(Overlord)

Gen5({reader}, CarrierIn)
WaitFor({reader}, false)

Set(keepOldGrowthPlate, false)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{copyPlateMethod})
Timer(120)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

// Put plate in reader for single OD read, without seal
Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader No Seal.ovp, [Gen5.Reader.Name] "{reader}")
WaitFor(Overlord)

Gen5({reader}, RunExp, {singleReadProtocol}, {experimentId}_single-OD_{lineage}-{plateNum}_0, {dataDirectory})
Timer({singleReadProtocolTime})
WaitFor(Timer)


//Then, seal plate and put it back in for the repeatProtocol
Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to Reader and Seal.ovp, [Gen5.Reader.Name] "{reader}" [Gen5.Reader2.Name] "{reader}")
WaitFor(Overlord)

Gen5({reader}, RunExp, {repeatProtocol}, {experimentId}_growth-plate_{lineage}-{plateNum}, {dataDirectory})
GetTimeNow(readStartTime)

If({lineage} == 1, Math(readEndTime1, {readStartTime} + {repeatProtocolTime}))
If({lineage} == 2, Math(readEndTime2, {readStartTime} + {repeatProtocolTime}))
