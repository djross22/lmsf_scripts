//preps and runs Plate 2 for each set of plates (aka lineage)
//This method needs to have the reader variable set to a plate reader name
//        and the readEndTime variable set to either {readEndTime1} or {readEndTime2}

//Optionally, the alsoAddTips300 variable can be set to false. It is true by default, for backward compatibility.
Set(alsoAddTips300, true)

#InsertVariables

//Move new plate and tips before WaitFor(Timer) so user can make sure there is no Overlord error
If({lineage} == 1, Set(clearWaste, false))
If({lineage} == 2, Set(clearWaste, true))

If({alsoAddTips300} == true, Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack} [STAR.Plate.Handling.Labware.Name] "Tips300" [STAR.Tip.300.Index] {tips300Index} [STAR.Tip.ClearWaste] "{clearWaste}"))
If({alsoAddTips300} != true, Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack}))
WaitFor(Overlord)

Math(platesInStack, {platesInStack} - 1)
If({alsoAddTips300} == true, Math(tips300Index, {tips300Index} - 1))

//Wait for reader to get close to finished before starting pipetting method
Math(pipetteStartTime, {readEndTime} - 2700)
Timer({pipetteStartTime})
WaitFor(Timer)

Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{lineage}-2.csv)
Set(dilutionFactor, 50)
//gradPlateNum is the number of the gradient plate within a plate lineage, used to set pipetting volumes so that final inducer concentration is correct
Set(gradPlateNum, 1)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{nonSelectiveGradientMethod})
Timer({nonSelectivePlateTime})
WaitFor(Timer)
WaitFor(S-Cell-STAR)

Math(preHeatStartTime, {readEndTime} - {preHeatPlateTime})
Timer({preHeatStartTime})
WaitFor(Timer)

RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{preHeatPlateMethod})
Timer(600)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

WaitFor({reader})
Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{reader}")
WaitFor(Overlord)

Gen5({reader}, CarrierIn)
WaitFor({reader}, false)

Set(keepOldGrowthPlate, false)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{copyPlateMethod})
Timer(180)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{reader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({reader}, RunExp, {plate2Protocol}, {experimentId}_growth-plate_{lineage}-2, {dataDirectory})
GetTimeNow(readStartTime)

If({lineage} == 1, Math(readEndTime1, {readStartTime} + {plate2ProtocolTime}))
If({lineage} == 2, Math(readEndTime2, {readStartTime} + {plate2ProtocolTime}))


