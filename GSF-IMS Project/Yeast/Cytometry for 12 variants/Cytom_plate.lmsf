//preps and runs the Cytometry Plate for each set of plates (aka lineage)
//This method needs to have the reader variable set to a plate reader name
//        and the readEndTime variable set to either {readEndTime1} or {readEndTime2}
#InsertVariables

ReadScript(C:\Users\PAA\Documents\LMSF Scheduler\Common protocol scripts\Need Tips.lmsf, tips300Need = 96, clearTipRack = true)

//Set up timer to wait for reader to get close to finished before starting pipetting method
Math(cytometryStartTime, {readEndTime} - 1300)

Timer({cytometryStartTime})
WaitFor(Timer)
//cytometryPlateNum tells Hamilton Venus which cytometry plate to use
Set(cytometryPlateNum, {lineage})
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{cytometryPrepMethod})
Timer(300)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

Timer({readEndTime})
WaitFor(Timer)
WaitFor({reader})
Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{reader}")
WaitFor(Overlord)

Gen5({reader}, CarrierIn)
WaitFor({reader}, false)

Set(worklistFile, {dataDirectory}\{experimentId}_cytom-plate_{lineage}-{plateNum}.csv)
Set(keepOldGrowthPlate, true)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{cytometryTransferMethod})
Timer(420)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

//Move old GP back to reader to continue reading for 16 hours
WaitFor({reader}, false)

Gen5({reader}, CarrierOut)
WaitFor({reader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{reader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Math(growOutPlateNum, {plateNum} + 1)
Gen5({reader}, RunExp, {finishProtocol}, {experimentId}_growth-plate_{lineage}-{growOutPlateNum}, {dataDirectory})
GetTimeNow(readStartTime)

If({lineage} == 1, Math(readEndTime1, {readStartTime} + {finishProtocolTime}))
If({lineage} == 2, Math(readEndTime2, {readStartTime} + {finishProtocolTime}))
