//isPipetting is a variable passed to the Hamilton Venus method; if isPipetting is true, the STAR will pipette liquids; otherwise the STAR just moves plates around
Set(isPipetting, true)
//isDebugging controls whether or not user debugging prompts/messages are displayed (both in LMSF and Hamilton)
Set(isDebugging, false)

//No DNA extraction for this version of the script
Set(keepOldGrowthPlate, false)

//manualSeal controls whether sealing is manual or automatic
//Set(manualSeal, Yes)
Set(manualSeal, No)

/////////////////////////////////////////////////////////////////
//Read in commonly used directories
ReadScript(C:\Users\PAA\Documents\LMSF Scheduler\Common protocol scripts\Set Common Directories.lmsf)
/////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////
//Set directories for  .hsl, .opv, and .prt files that are specifc to this experiment
Set(hamiltonMethodDir, C:\Program Files (x86)\Hamilton\Methods\GSF-IMS Project\Yeast Methods\Cytometry for 6 variants)
Set(ovpDirectory, C:\Program Files (x86)\PAA\Overlord3\Procedures\GSF-IMS\E. coli\Fitness Selection 1)
Set(scriptDir, C:\Users\PAA\Documents\LMSF Scheduler\GSF-IMS Project\Yeast\Cytometry for 6 variants)
/////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////
//Hamilton Venus Methods, in the order that they get run
Set(startMethod, Uniform non-selective plate prep plus yeast.6 variants.hsl)
Set(nonSelectiveGradientMethod, Inducer gradient plate prep.hsl)
Set(preHeatPlateMethod, Pre-heat growth plate.hsl)
//Plate pre-heating method takes 12m:16s to run
Set(preHeatPlateTime, 736)
Set(copyPlateMethod, Dilute-copy plate x-fold with 96-head.hsl)
Set(cytometryPrepMethod, Prep cytometry plate.hsl)
Set(cytometryTransferMethod, Transfer cells from old GP to Cytometry Plate.hsl)
//Venus method constrains 200<=cultureVolume<=450
Set(cultureVolume, 450)
Set(beadZShift, 0.2)
Set(beadXYShift, 1.7)


/////////////////////////////////////////////////////////////////
//Gen5 protcols to be run and associated timers:
Set(overNightProtocol, {gen5ProtDir}\GSF-IMS-Yeast\Growth Plate w Membrane 12h.30C.1C.prt)
//overNightProtocolTime = nominal run time + 600
Set(overNightProtocolTime, 43800)

Set(repeatProtocol, {gen5ProtDir}\GSF-IMS-Yeast\Growth Plate w Membrane 6h.30C.1C.prt)
//repeatProtocolTime = nominal run time + 600
Set(repeatProtocolTime, 22200)

//Gen5 protocol for testing readers at beginning of run to give consistent starting state
Set(initProtocol, {gen5ProtDir}\Growth Plate w Membrane short test.30C.prt)
/////////////////////////////////////////////////////////////////

//Set readers to be used
Set(mainReader, Epoch1)

/////////////////////////////////////
//This section sets shorter Gen5 protocols for testing, set isTesting to false for actual experiment
Set(isTesting, false)
GetUserYesNo(isTesting, Testing?, Is this a test run (short Gen5 protocols)?)

If({isTesting}==Yes, Set(overNightProtocol, {gen5ProtDir}\GSF-IMS-Yeast\Growth Plate w Membrane 15m.30C.1C.prt))
//overNightProtocolTime = nominal run time + 300
If({isTesting}==Yes, Set(overNightProtocolTime, 900))
If({isTesting}==Yes, Set(repeatProtocol, {gen5ProtDir}\GSF-IMS-Yeast\Growth Plate w Membrane 10m.30C.1C.prt))
//repeatProtocolTime = nominal run time + 300
If({isTesting}==Yes, Set(repeatProtocolTime, 600))
/////////////////////////////////////////

/////////////////////////////////////////
//Set any other short-hand dictionary keys that will be used repeatedly
Set(getNewGrowthPlate, {ovpCommDirectory}\Labware\Move Tips and New Growth Plate to STAR.ovp)
/////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////
//User input and Overlord initialization section

StartPrompt(E. Coli Fitness Selection, {scriptDir}\Cytometry.6 yeast-list.txt)

UserPrompt(Check STAR Hamilton Run Control, Make sure that the Hamilton Run Control program on the STAR computer is NOT in simulation mode.)
UserPrompt(Check STAR Hamilton Tips, Make sure there are at least 38 1000 uL tips in the tip supports on the deck of the STAR., {lmsfImageDirectory}\Tip supports 1000.PNG)
UserPrompt(Check STAR Hamilton Tips, Make sure there are at least 104 300 uL tips in the tip supports on the deck of the STAR., {lmsfImageDirectory}\Tip supports 300.PNG)

Set(msg2, Make sure there is at least one rack of 300 uL tips in the PAA Carousel (hotel no. 2).\n)
UserPrompt(Tips, {msg2}, {lmsfImageDirectory}\Tip Hotel 2.png)
Set(tips300Index, 1)

NewXML(growth plate prep)

Overlord({ovpCommDirectory}\Initialize Robot Carousel Sealer and Pealer.ovp)

Get(user, user)

Get(media, media)

UserPrompt(Strain List, Open the file strain_plasmids.txt and edit it to contain the appropriate information for this experiment.\nStrains for rows B-G of the growth plate.)
ImportDictionary({scriptDir}\strain_plasmids.txt)

AddXML(strains, strain)
AddXML(strain, strainId, {strainB})
AddXML(strains, strain)
AddXML(strain, strainId, {strainC})
AddXML(strains, strain)
AddXML(strain, strainId, {strainD})
AddXML(strains, strain)
AddXML(strain, strainId, {strainE})
AddXML(strains, strain)
AddXML(strain, strainId, {strainF})
AddXML(strains, strain)
AddXML(strain, strainId, {strainG})

Get(additive, inducer, Select the inducer used for the experiment:)
Get(concentration, inducerStock, Enter the concentration of the {inducer} starting stock:)

Get(note, note)

GetExpID({startDate}_{inducer}-Cytom-6-yeast)

//review experiment ok/abort dialog
Set(msg1, Review the following, then click 'OK' to accept or 'Abort' to abort and start over:\n\n)
Set(msg2, \tMedia: \t\t\t{media}\n\n)
Set(msg3, \tYeast strains:\n)
Set(msg4, \t\tRow B: \t{strainB}\n)
Set(msg5, \t\tRow C: \t{strainC}\n)
Set(msg6, \t\tRow D: \t{strainD}\n)
Set(msg7, \t\tRow E: \t{strainE}\n)
Set(msg8, \t\tRow F: \t{strainF}\n)
Set(msg9, \t\tRow G: \t{strainG}\n\n)

Set(msg10, \tInducer: \t\t\t{inducer}, stock concentration: {inducerStock}\n\n)
Set(msg11, \tExperiment ID: \n\t\t{experimentId}\n \tData directory: \n\t\t{dataDirectory}\n\n \tNotes:\n\t\t{note})
UserPrompt(Review Experiment Details: {experimentId}, {msg1}{msg2}{msg3}{msg4}{msg5}{msg6}{msg7}{msg8}{msg9}{msg10}{msg11})

Set(msg1, Set up the STAR deck layout as shown.)
UserPrompt(STAR Deck Layout, {msg1}, {scriptDir}\STAR Deck Layout.1 cytometry plate.PNG, 1000)

Set(msg1, Close the front cover on the STAR.\n)
Set(msg2, Ensure that the Robot arm is in a safe position and that the Guard Override key is in the 'Off' position.\n)
Set(msg3, Check that all the doors of the S-Cell system are closed.\n)
Set(msg4, Then click 'OK' to start the experiment.\n)
UserPrompt(Ready to Start, {msg1}{msg2}{msg3}{msg4}, {lmsfImageDirectory}\dialog-warning.png)

//Lid movement test
UserPrompt(Lid Movement Test, Switch to the STAR computer and observe the lid movement test.)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\Pre-run lid test.hsl)
Timer(100)
WaitFor(Timer)
WaitFor(S-Cell-STAR)
UserPrompt(Lid Movement Test, If the STAR successfully moved the lids click 'OK' to start the experiment.)
// end lid movement test


WaitFor(Overlord)
//End User input and Overlord initialization section
//////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////////
//Run the protocol
////////////////////////////////////////////////////////////////////////////////////

//--- 0 ------------------------------------------------------------------------------

//Test readers while 1st plate is pipetting
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)
Set(msg, Plate reader {mainReader} should have its carrier out. \nVerify that the carrier is out and empty, then Click 'OK' to continue.)
UserPrompt(Check Readers, {msg})
Gen5({mainReader}, RunExp, {initProtocol}, pre-run-test, {dataDirectory})

Set(msg, Switch to STAR computer to edit tip counters, then Click 'OK' to continue.)
UserPrompt(Check Readers, {msg})
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonTipDir}\Edit Tip Counters.hsl)
WaitFor(S-Cell-STAR)

//--- Plate 1 ------------------------------------------------------------------------------
Set(plateNum, 1)
Set(platesInStack, 2)

Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack})
Math(platesInStack, {platesInStack} - 1)
WaitFor(Overlord)

Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{plateNum}.csv)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{startMethod})
Timer(600)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

WaitFor({mainReader}, false)

Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{mainReader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({mainReader}, RunExp, {overNightProtocol}, {experimentId}_growth-plate_{plateNum}a, {dataDirectory})
GetTimeNow(readStartTime)
Math(readEndTime, {readStartTime} + {overNightProtocolTime})

//--- Plate 2 ----------------------------------------------------------------------------- 
Math(plateNum, {plateNum} + 1)

//Move new plate and tips before WaitFor(Timer) so user can make sure there is no Overlord error
Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {platesInStack} [STAR.Plate.Handling.Labware.Name] "Tips300" [STAR.Tip.300.Index] {tips300Index})
Math(tips300Index, {tips300Index} - 1)
Math(platesInStack, {platesInStack} - 1)
WaitFor(Overlord)

//Wait for reader to get close to finished before starting pipetting method
Math(pipetteStartTime, {readEndTime} - 2220)
Timer({pipetteStartTime})
WaitFor(Timer)

Set(worklistFile, {dataDirectory}\{experimentId}_growth-plate_{plateNum}.csv)
Set(dilutionFactor, 40)
//gradPlateNum is the number of the gradient plate within a plate lineage, used to set pipetting volumes so that final inducer concentration is correct
Set(gradPlateNum, 1)
//sequenceNum is the total number of gradient plates mixed
//    used by the Hamilton Venus program to determine which column in the Abgene plate to use for starting the inducer gradient
Set(sequenceNum, 1)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{nonSelectiveGradientMethod})
WaitFor(S-Cell-STAR)

Math(preHeatStartTime, {readEndTime} - {preHeatPlateTime})
Timer({preHeatStartTime})
WaitFor(Timer)

RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{preHeatPlateMethod})
Timer(600)
WaitFor(Timer)
WaitFor(S-Cell-STAR)

WaitFor({mainReader})
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{mainReader}")
WaitFor(Overlord)

Gen5({mainReader}, CarrierIn)
WaitFor({mainReader}, false)

Set(keepOldGrowthPlate, false)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{copyPlateMethod})
WaitFor(S-Cell-STAR)

Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{mainReader}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({mainReader}, RunExp, {repeatProtocol}, {experimentId}_growth-plate_{plateNum}a, {dataDirectory})
GetTimeNow(readStartTime)
Math(readEndTime, {readStartTime} + {repeatProtocolTime})



//--- Cytometry Plate (already on the STAR deck) ---------------------------------------------------------------------------------

//Set up timer to wait for reader to get close to finished before starting pipetting method
Math(cytometryStartTime, {readEndTime} - 1300)

Timer({cytometryStartTime})
WaitFor(Timer)
//cytometryPlateNum tells Hamilton Venus which cytometry plate to use
Set(cytometryPlateNum, 1)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{cytometryPrepMethod})
WaitFor(S-Cell-STAR)

WaitFor({mainReader})
Gen5({mainReader}, CarrierOut)
WaitFor({mainReader}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{mainReader}")
WaitFor(Overlord)

Gen5({mainReader}, CarrierIn)
WaitFor({mainReader}, false)

Set(keepOldGrowthPlate, true)
Set(worklistFile, {dataDirectory}\{experimentId}_cytom-plate_{plateNum}.csv)
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{cytometryTransferMethod})
WaitFor(S-Cell-STAR)


//--- Clean up --------------------------------------------------------------------------------------

Overlord({ovpCommDirectory}\Sealer Heater Off.ovp)
WaitFor(Overlord)

SaveXML()

CopyRemoteFiles()
