Math(growthPlateNumber, {growthPlateNumber} + 1)

If({tipsClear}==true, Overlord(C:\Program Files (x86)\PAA\Overlord3\Procedures\Common\Labware\Move Tips from STAR to Waste.ovp))
If({tipsClear}==true, WaitFor(Overlord))

//Tip counters check
ReadScript(C:\Users\PAA\Documents\LMSF Scheduler\Common protocol scripts\Need Tips.lmsf, tips1000Need = 16, userEditCounters=false)

//load new GP and 300 uL tip rack onto HamSTAR
//updates tips and plates index
Overlord({getNewGrowthPlate}, [Carousel.Stack.7.Labware.Count] {growthPlatesInStack} [STAR.Plate.Handling.Labware.Name] "Tips300" [STAR.Tip.300.Index] {tips300Index})
Math(growthPlatesInStack, {growthPlatesInStack} - 1)
Math(tips300Index, {tips300Index} - 1)
WaitFor(Overlord)

//Waits for timer set in main scripts
//plate 1 uses overnightWait 
//plates 2-9 use repeatWait
WaitFor(Timer)

//runs nonSelectivePlateMethod (Uniform non-selective plate prep_Vnatriegens.hsl), then waits for STAR to finish
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{nonSelectivePlateMethod})
WaitFor(S-Cell-STAR)

If({glycStockBoolean} == true, UserPrompt(Load Glycerol Plates, Add glycerol plates to deck and click 'OK', {protocolDir}\Deck layout_glycerol stock plates.png, 1000))

//waits for reader to finish
WaitFor({reader1}, false)

Gen5({reader1}, CarrierOut)
WaitFor({reader1}, false)

//Starts 5 min shaking in reader while plate preheats
Gen5({reader1}, RunExp, {preheatProtocol}, {experimentId}_growth-plate_{growthPlateNumber}_shakedelay, {dataDirectory})

//runs preheatGPMethod (Pre-heat growth plate_Vnatriegens.hsl), then waits for STAR to finish
RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{preheatGPMethod})
WaitFor(S-Cell-STAR)

//waits for reader to finish
WaitFor({reader1}, false)

Gen5({reader1}, CarrierOut)
WaitFor({reader1}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from Reader to STAR.ovp, [Gen5.Reader.Name] "{reader1}")
WaitFor(Overlord)

RemoteHam(S-Cell-STAR, RunMethod, {hamiltonMethodDir}\{copyPlateMethod})
WaitFor(S-Cell-STAR)

Gen5({reader1}, CarrierOut)
WaitFor({reader1}, false)

Overlord({ovpCommDirectory}\Move Growth Plate from STAR to Reader.ovp, [Gen5.Reader.Name] "{reader1}" [Sealer.ManualSeal] "{manualSeal}")
WaitFor(Overlord)

Gen5({reader1}, RunExp, {repeatProtocol}, {experimentId}_growth-plate_{growthPlateNumber}, {dataDirectory})
//WaitFor({reader1})
GetTimeNow(readerStartTime)

//determine when to end timer
Math(endTimer, {readerStartTime} + {repeatWait})

If({glycStockBoolean} == true, UserPrompt(Remove Glycerol Plates, Remove glycerol stock plates from deck then seal and store the plate at -80. Click 'OK' to continue, {protocolDir}\Clear deck_glycerol stock plates.png, 1000))

// Sets variable for prepping glycerol stock to false
Set(glycStockBoolean, false)

//Wait for that timer
Timer({endTimer})


